Your first log analysis goal is to ensure you're getting data about all active virtual machines in your network. You want to identify machines that stop sending data, so that you can investigate and ensure you have full visibility of all active virtual machines.

Here, you'll run KQL queries to retrieve and transform data from the `heartbeat` table to obtain insights about the status of machines in your environment.  

## Set analysis goals and assess log data

Azure Monitor uses Azure Monitor Agent to collect data about activities and operating system processes running inside virtual machines. Some of the older machines in your environment still use the legacy Log Analytics Windows and Linux agents, which Azure Monitor is deprecating. Azure Monitor Agent and Log Analytics Agent log information about virtual machine health to the `Heartbeat` table once a minute.

1. What information do you need to determine which machines have stopped sending data?

    - All machines that have recently logged data, but have not logged data as expected in the past few minutes.
    - For further analysis, it's useful to know which virtual machine agent is running on each machine.
 
1. Which data in the `Heartbeat` table is relevant to your analysis and how do you want to transform and organize this data?
    
    This screenshot shows the result set of a simple `take 10` query on the `Heartbeat` table (the table has other columns that are not shown in the screenshot):    

    :::image type="content" source="../media/kql-log-analytics-heartbeat-table.png" alt-text="Screenshot showing the results of a take 10 query on the Heartbeat table with the TimeGenerated, Computer, Category, and OSType columns highlighted." lightbox="../media/kql-log-analytics-heartbeat-table.png":::

    You can see that the columns that hold relevant data are:

    | Columnn name | Description | Use in analysis |
    | --- | --- | --- | 
    | `TimeGenerated` | Indicates when the virtual machine generated each log. | You'll use this column twice in your analysis to: <ul><li>Identify machines that have been active recently (`where TimeGenerated >ago(48h)`).</li><li>Find the last log generated for each machine and check whether it was generated in the last few minutes (`max(TimeGenerated)`).</li></ul> | 
    | `Computer` |Unique identifier of the machine. |  | 
    | `Category` |The agent type - `Azure Monitor Agent` or `Direct Agent`, which represent the Log Analytics agent. The Log Analytics agent for Windows is also called OMS. | Change the `Direct Agent` value to `MMA` for Windows machines and `OMS` for Linux machines to simplify the results and facilitate further analysis, such as filtering. | 
    | `OSType` | The type of operating system running on the virtual machine. | | 

## Identify recently active machines that have stopped logging data

To identify recently active machines that have stopped logging data, write a query that lists the agents that have been active in the past 48 hours, but haven't logged data to the `Heartbeat` table in the last five minutes. 

1. Retrieve all logs from the past 48 hours:

    ```kusto
    Heartbeat // The table you’re querying
    | where TimeGenerated >ago(48h) // Time range for the query - in this case, logs generated in the past 48 hours
    ```

    The result set of this query likely includes numerous logs for each active machine.
    
1. Retrieve the last log generated by each machine and summarize by computer, agent type, and operating system:     

    ```kusto
    Heartbeat // The table you’re querying
    | where TimeGenerated >ago(48h) // Time range for the query - in this case, logs generated in the past 48 hours
    | summarize max(TimeGenerated) by Computer, AgentType=Category, OSType // Summarizes records by unique combination of computer, agent type, and operating system, and retrieves the last record generated for each combination
    ```
    
    In the `summarize` line, you've renamed the `Category` column to `AgentType`, which better describes the information you're looking at in the column for this analysis.


1. Retrieve the last log generated by a machine only if that record was generated more than five minutes ago:

    ```kusto
    Heartbeat // The table you’re querying
    | where TimeGenerated >ago(48h) // Time range for the query - in this case, logs generated in the past 48 hours
    | summarize max(TimeGenerated) by Computer, AgentType=Category, OSType // Summarizes records by unique combination of computer, agent type, and operating system, and retrieves the last record generated for each combination
    | where max_TimeGenerated < ago(5m) // Filters away all records generated in the last five minutes
    ```

    The result set of this query includes the last log generated by all machines that logged data in the past 48 hours, but doesn't include logs generated in the past five minutes. In other words, any machine that logged data in the last five minutes isn't included in the result set.

    You now have the data you're looking for: a list of all machines that logged data in the last 48 hours, but haven't been logging data as expected in the last five minutes. The result set consists of the set of computers you want to investigate further.

1. Manipulate the query results to present the information more clearly. 

    For instance, you decide to organize the logs by time generated - from the oldest to the newest - to see which computers have gone the longest time without logging data. 

    The `Direct Agent` value in the AgentType column tells you that the Log Analytics Agent is running on the machine. The Log Analytics Agent for Windows is also called OMS. The Log Analytics Agent for Linux is also call MMS. Renaming the `Direct Agent` value to `MMA` for Windows machines and `OMS` for Linux machines simplifies the results and facilitates further analysis, such as filtering.

    ```kusto
    Heartbeat // The table you’re querying
    | where TimeGenerated >ago(48h) // Time range for the query - in this case, logs generated in the past 48 hours 
    | summarize max(TimeGenerated) by Computer,AgentType=Category, OSType // Summarizes records by unique combination of computer, agent type, and operating system, and retrieves the last record generated for each combination
    | where max_TimeGenerated < ago(5m) // Filters away all records generated in the last five minutes
    | extend AgentType= iif(AgentType == "Direct Agent" and OSType =="Windows", "MMA", AgentType) // Changes the AgentType value from "Direct Agent" to "MMA" for Windows machines
    | extend AgentType= iif(AgentType == "Direct Agent" and OSType =="Linux", "OMS", AgentType) // Changes the AgentType value from "Direct Agent" to "OMS" for Linux machines
    | order by max_TimeGenerated asc // Sorts results by max_TimeGenerated from oldest to newest
    | project-reorder max_TimeGenerated,Computer,AgentType,OSType  // Reorganizes the order of columns in the result set
    ```

    You can use `max_TimeGenerated` to correlate the last heartbeat of the machine that stopped reporting with machine logs or other environmental events that occurred around the same time. Correlating these logs can help in analyzing the reason the machined stopped sending data.

## List the agents and agent versions running on recently active machines

Understanding which agent versions are running on your machines can be useful in analyzing the root cause of problems and helps you decide which machines you need to update to a new agent version.

1. Retrieve all logs from the past 10 minutes: 

    ```kusto    
    Heartbeat // The table you’re querying
    | where TimeGenerated >ago(10m) // Time range for the query - in this case, logs generated in the past 10 minutes
    ```

1. Rename the `Category` column name and `Direct Agent` values:
    
    ```kusto    
    Heartbeat // The table you’re querying
    | where TimeGenerated >ago(10m) // Time range for the query - in this case, logs generated in the past 10 minutes
    | project-rename AgentType=Category // Changes the name of the "Category" column to "AgentType"
    | extend AgentType= iif(AgentType == "Direct Agent" and OSType =="Windows", "MMA", AgentType) // Changes the AgentType value from "Direct Agent" to "MMA" for Windows machines
    | extend AgentType= iif(AgentType == "Direct Agent" and OSType =="Linux", "OMS", AgentType) // Changes the AgentType value from "Direct Agent" to "OMS" for Linux machines
    ```

    The result set of this query likely includes numerous logs for each active machine.
    
1. Find unique combinations of agent type, agent version, and operating system type, and list all computers running each combination of agent type and agent version: 

    ```kusto    
    Heartbeat // The table you’re querying
    | where TimeGenerated >ago(10m) // Time range for the query - in this case, logs generated in the past 10 minutes
    | project-rename AgentType=Category // Changes the name of the "Category" column to "AgentType"
    | extend AgentType= iif(AgentType == "Direct Agent" and OSType =="Windows", "MMA", AgentType) // Changes the AgentType value from "Direct Agent" to "MMA" for Windows machines
    | extend AgentType= iif(AgentType == "Direct Agent" and OSType =="Linux", "OMS", AgentType) // Changes the AgentType value from "Direct Agent" to "OMS" for Linux machines
    | summarize ComputersList=make_set(Computer) by AgentVersion=Version, AgentType, OSType // Summarizes the result set by unique combination of agent type, agent version, and operating system, and lists the set of all machines running the specific agent version
    ```

    You now have the data you're looking for: a list of unique combinations of agent type and agent version and the set of all recently active machines that are running a specific version of each agent. 

1. Transform the query results to present the information more clearly.
    
    For instance, sort the results by agent name:  

    ```kusto    
    Heartbeat // The table you’re querying
    | where TimeGenerated >ago(10m) // Time range for the query - in this case, logs generated in the past 10 minutes
    | project-rename AgentType=Category // Changes the name of the "Category" column to "AgentType"
    | extend AgentType= iif(AgentType == "Direct Agent" and OSType =="Windows", "MMA", AgentType) // Changes the AgentType value from "Direct Agent" to "MMA" for Windows machines
    | extend AgentType= iif(AgentType == "Direct Agent" and OSType =="Linux", "OMS", AgentType) // Changes the AgentType value from "Direct Agent" to "OMS" for Linux machines
    | summarize ComputersList=make_set(Computer) by AgentVersion=Version, AgentType, OSType // Summarizes the result set by unique combination of agent type, agent version, and operating system, and lists the set of all machines running the specific agent version
    | order by AgentType desc // Sorts results by agent name in descending alphabetical order
    ```

<!-- 4. Validation -------------------------------------------------------------------------------------------

    Goal: Enables the learner to evaluate if they completed the exercise correctly. This feedback is critical for learning.

    Structure:
        1. H2 of "Check your work".
        2. An introductory paragraph describing how they'll validate their work at a high level.
        3. Numbered steps (if the learner needs to perform multiple steps to verify if they were successful).
        4. Video of an expert performing the exact steps of the exercise (optional).

    Example:
         "At this point, the app is scanning Twitter every minute for tweets containing the search text. To verify the app is running and working correctly, we'll look at the Runs history table."
             "1. Select Overview in the navigation menu.
              2. Select Refresh once a minute until you see a row in the Runs history table.
              ...
              6. Examine the data in the OUTPUTS section. For example, locate the text of the matching tweet."
-->

## Check your work
<!-- Introduction paragraph -->
1. <!-- Step 1 (if multiple steps are needed) -->
1. <!-- Step 2 (if multiple steps are needed) -->
1. <!-- Step n (if multiple steps are needed) -->
Optional "exercise-solution" video

<!-- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -->

<!-- Do not add a unit summary or references/links -->
